pkgname=(qubes-vm-gui qubes-vm-pulseaudio)
pkgrel=10
epoch=
pkgdesc="The Qubes GUI Agent for AppVMs"
arch=("x86_64")
url="http://qubes-os.org/"
license=('GPL')
groups=()
checkdepends=()
optdepends=()
provides=()
conflicts=()
replaces=()
backup=()
options=()
changelog=

source=()
md5sums=()

noextract=()

# Use envvar $PA_VER_FULL to specify pulseaudio version manually
# e.g. PA_VER_FULL=16.1 makepkg -s
if [ -z $PA_VER_FULL ]
then
    pa_ver=$(pkg-config --modversion libpulse)
    if [ ! $? -eq 0 ]; then
        echo "Cannot find libpulse version with pkg-config."
        echo "Please specify version manually with PA_VER_FULL."
        exit 1
    fi
    pa_ver=$(echo $pa_ver | cut -f 1 -d "-")
    export PA_VER_FULL=$pa_ver
else
    pa_ver=$PA_VER_FULL
fi

makedepends=(pkg-config make gcc patch git automake autoconf libtool
			"pulseaudio=$pa_ver"
			xorg-server-devel xorg-util-macros libxcomposite libxt pixman
			qubes-vm-gui-common qubes-libvchan qubes-db-vm
			)


if [ -e archlinux ] # on no, at repo root
then
    echo Do not run 'makepkg' at repo root! >&2
    cd archlinux
fi

pkgver=$(cat $startdir/../version)

clean() {
    rm -r $srcdir/* || true
}

build() {

rm $srcdir/PKGBUILD-z-qubes-session.sh || true
cp $startdir/PKGBUILD-z-qubes-session.sh $srcdir/
for source in Makefile appvm-scripts gui-common include pulse gui-agent common xf86-input-mfndev xf86-video-dummy xf86-qubes-common window-icon-updater version; do
  if [ -e "$source" ]; then rm -r "$source"; fi
  cp -r $startdir/../$source $source
done

pulsecore_dir="pulse/pulsecore-$pa_ver"
if [ ! -d $pulsecore_dir ]; then
    echo "Invalid pulsecore_dir: $pulsecore_dir"
    exit 2
fi
if [ -e pulse/pulsecore ]; then rm -r pulse/pulsecore; fi
ln -s $(realpath "$pulsecore_dir") pulse/pulsecore

# Bug fixes : /var/run/console depends on pam_console, which is fedora specific
# As a consequece, /var/run/console does not exists and qubes-gui-agent will always fail
sed 's:ExecStartPre=/bin/touch:#ExecStartPre=/bin/touch:' -i appvm-scripts/qubes-gui-agent.service
# Ensure that qubes-gui-agent starts after user autologin
sed 's/After=\(.*\)qubes-misc-post.service/After=\1qubes-misc-post.service getty.target/' -i appvm-scripts/qubes-gui-agent.service

make appvm

}

package_qubes-vm-gui() {

depends=('xorg-xinit' 'libxcomposite' 'zenity' 'qubes-libvchan' 'python-xcffib'
			# Xorg dependencies are on specific ABI versions: https://www.x.org/wiki/XorgModuleABIVersions/
			# These can also be verified with pacman -Qi xorg-server (Provides)
			# There is however a discrepency if verifying via pkg-config --variable abi_videodrv xorg-server
			'X-ABI-VIDEODRV_VERSION<=25.2' 'X-ABI-XINPUT_VERSION<=24.4' 'X-ABI-EXTENSION_VERSION=10.0'
			)
install=PKGBUILD.install

make install-rh-agent DESTDIR=$pkgdir LIBDIR=/usr/lib USRLIBDIR=/usr/lib SYSLIBDIR=/usr/lib

# Specific to archlinux: qubes session needs to be the last started script when Xorg starts.
# For Fedora Qubes devs team choosed to populate it in /etc/sysconfig/desktop, but this is not supported by Archlinux
# Using z-qubes-session allows it to be the last started script in xinitrc.d...
mkdir -p $pkgdir/etc/X11/xinit/xinitrc.d/
install -D $srcdir/PKGBUILD-z-qubes-session.sh $pkgdir/etc/X11/xinit/xinitrc.d/z-qubes-session.sh

}

package_qubes-vm-pulseaudio() {

pkgdesc="Pulseaudio support for Qubes VM"
depends=('alsa-lib' 'alsa-utils' 'pulseaudio-alsa' "pulseaudio=$pa_ver")
install=PKGBUILD-pulseaudio.install

make install-pulseaudio DESTDIR=$pkgdir PA_VER=$pa_ver LIBDIR=/usr/lib USRLIBDIR=/usr/lib SYSLIBDIR=/usr/lib

}

# vim:set ts=2 sw=2 et:
