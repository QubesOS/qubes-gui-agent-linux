# Maintainer: Frédéric Pierret (fepitre) <frederic@invisiblethingslab.com>

pkgname=(qubes-vm-gui qubes-vm-pulseaudio pipewire-qubes)
pkgver=@VERSION@
pkgrel=@REL@
pkgdesc="The Qubes GUI Agent for AppVMs"
arch=("x86_64")
url="http://qubes-os.org/"
license=('GPL')
makedepends=(
    pkg-config
    make
    gcc
    patch
    git
    automake
    autoconf
    libtool
    'pulseaudio<=16.1'
    'pipewire>=1:0.3.53-2'
    'pipewire<1:0.4'
    xorg-server-devel
    xorg-util-macros
    libxcomposite
    libxt
    pixman
    lsb-release
    qubes-vm-gui-common
    qubes-libvchan
    qubes-db-vm
)
_pkgnvr="${pkgname}-${pkgver}-${pkgrel}"
source=("${_pkgnvr}.tar.gz")
sha256sums=(SKIP)

build() {
    _pa_ver="$( (pkg-config --modversion libpulse 2>/dev/null || echo 0.0) | cut -f 1 -d "-" )"

    cd "${_pkgnvr}"
    rm -f pulse/pulsecore
    ln -s "pulsecore-${_pa_ver}" pulse/pulsecore

    # Bug fixes : /var/run/console depends on pam_console, which is fedora specific
    # As a consequence, /var/run/console does not exists and qubes-gui-agent will always fail
    sed 's:ExecStartPre=/bin/touch:#ExecStartPre=/bin/touch:' -i appvm-scripts/qubes-gui-agent.service
    # Ensure that qubes-gui-agent starts after user autologin
    sed 's/After=\(.*\)qubes-misc-post.service/After=\1qubes-misc-post.service getty.target/' -i appvm-scripts/qubes-gui-agent.service

    make appvm
}

package_qubes-vm-gui() {
    depends=(
    'xorg-xinit'
    'libxcomposite'
    'zenity'
    'qubes-libvchan'
    'python-xcffib'
    # Xorg dependencies are on specific ABI versions: https://www.x.org/wiki/XorgModuleABIVersions/
    # These can also be verified with pacman -Qi xorg-server (Provides)
    # There is however a discrepancy if verifying via pkg-config --variable abi_videodrv xorg-server
    'X-ABI-VIDEODRV_VERSION<=25.2'
    'X-ABI-XINPUT_VERSION<=24.4'
    'X-ABI-EXTENSION_VERSION=10.0'
    )
    install="archlinux/PKGBUILD.install"

    cd "${_pkgnvr}"

    make install-rh-agent \
        DESTDIR="${pkgdir}" \
        LIBDIR=/usr/lib \
        USRLIBDIR=/usr/lib \
        SYSLIBDIR=/usr/lib

    # Specific to archlinux: qubes session needs to be the last started script when Xorg starts.
    # For Fedora Qubes devs team chose to populate it in /etc/sysconfig/desktop, but this is not supported by Archlinux
    # Using z-qubes-session allows it to be the last started script in xinitrc.d...
    mkdir -p "${pkgdir}/etc/X11/xinit/xinitrc.d/"
    install -D archlinux/PKGBUILD-z-qubes-session.sh "${pkgdir}/etc/X11/xinit/xinitrc.d/z-qubes-session.sh"

}

package_qubes-vm-pulseaudio() {
    pkgdesc="Pulseaudio support for Qubes VM"
    depends=('alsa-lib' 'alsa-utils' 'pulseaudio-alsa' 'pulseaudio<=16.1')
    conflicts=('qubes-core-agent<4.2.5')
    install=archlinux/PKGBUILD-pulseaudio.install
    _pa_ver="$( (pkg-config --modversion libpulse 2>/dev/null || echo 0.0) | cut -f 1 -d "-" )"

    cd "${_pkgnvr}"
    make install-pulseaudio \
        DESTDIR="${pkgdir}" \
        PA_VER="${_pa_ver}" \
        LIBDIR=/usr/lib \
        USRLIBDIR=/usr/lib \
        SYSLIBDIR=/usr/lib
}

package_pipewire-qubes () {

    pkgdesc='PipeWire support for Qubes VM.  This replaces the legacy PulseAudio module.'
    depends=('pipewire>=1:0.3.53-2' 'pipewire<1:0.4')
    replaces=('qubes-vm-pulseaudio<=4.1.26-10')
    install=archlinux/PKGBUILD-pipewire-qubes.install

    cd "${_pkgnvr}"
    make install-pipewire \
        "DESTDIR=$pkgdir" \
        LIBDIR=/usr/lib \
        USRLIBDIR=/usr/lib \
        SYSLIBDIR=/usr/lib
}
